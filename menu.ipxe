#!ipxe

:start
echo
dhcp || goto netconfig
goto timesync

:netconfig
echo Network Configuration:
echo Available interfaces...
ifstat
imgfree
echo -n Set network interface number [0 for net0, defaults to 0]: ${} && read net
isset ${net} || set net 0
echo -n IP: && read net${net}/ip
echo -n Subnet mask: && read net${net}/netmask
echo -n Gateway: && read net${net}/gateway
echo -n DNS: && read dns
ifopen net${net}
goto timesync

:timesync
ntp pool.ntp.org || echo NTP command is not available.
goto menu

:menu
menu iPXE v${version}
item --gap OS options
item boot_menu ${space} Install Ubuntu
item netboot ${space} netboot.xyz
item --gap
item --gap Debug
item netconfig ${space} Manual network configuration
item debug ${space} iPXE shell
item reboot ${space} Reboot
item leave ${space} Exit iPXE
choose option || goto start
goto ${option}

:leave
exit

:reboot
reboot

:debug
echo Type "exit" to return to menu
shell
goto menu

:netboot
chain --replace --autofree https://boot.netboot.xyz/menu.ipxe || echo HTTPS failed. Trying HTTP.
sleep 2
chain --replace --autofree http://boot.netboot.xyz/menu.ipxe || echo Connection failed.
sleep 3
goto menu

:boot_menu
menu iPXE v${version}
item --gap Options
item set_server ${space} Server: ${serverURL}
item set_preseed ${space} Preseed: ${preseedURL}
item --gap
item --gap Install
item boot_install ${space} Install
item boot_expert ${space} Expert Install
item boot_rescue ${space} Rescue
choose --default ${boot_option} boot_option || goto menu
goto ${boot_option}

:set_server
isset ${serverURL} || set serverURL http://${net0/gateway}:19000/netboot
menu iPXE v${version}
item set_server_manual ${space} Manual: ${serverURL}
item set_server_focal ${space} Ubuntu 20.04 LTS Focal Fossa amd64
item set_server_eoan ${space} Ubuntu 19.10 Eoan Ermine amd64
item set_server_bionic ${space} Ubuntu 18.04 LTS Bionic Beaver amd64
choose set_server_option || goto boot_menu
goto ${set_server_option}

:set_server_manual
echo -n Netboot server: ${}
read serverURL || goto boot_menu
goto boot_menu

:set_server_focal
set serverURL http://archive.ubuntu.com/ubuntu/dists/focal/main/installer-amd64/current/legacy-images/netboot
goto boot_menu

:set_server_eoan
set serverURL http://archive.ubuntu.com/ubuntu/dists/eoan/main/installer-amd64/current/images/netboot
goto boot_menu

:set_server_bionic
set serverURL http://archive.ubuntu.com/ubuntu/dists/bionic/main/installer-amd64/current/images/netboot
goto boot_menu

:set_preseed
isset ${preseedURL} || set preseedURL http://${net0/gateway}:18000/
echo -n Preseed URL: ${}
read preseedURL || goto boot_menu
set install_params priority=critical
set preseed_params auto=true interface=auto preseed/url=${preseedURL}
goto boot_menu

:boot_rescue
set install_params rescue/enable=true
goto boot_install

:boot_expert
set install_params priority=low
goto boot_install

:boot_install
imgfree
kernel ${serverURL}/ubuntu-installer/amd64/linux ${preseed_params} ${install_params} -- quiet initrd=initrd.gz
initrd ${serverURL}/ubuntu-installer/amd64/initrd.gz
boot
echo Exiting iPXE boot sequence
shell
sleep 5
